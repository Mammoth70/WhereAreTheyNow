plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

def versionMajor = 3
def versionMinor = 11
def versionPatch = 17

android {
    namespace 'ru.mammoth70.wherearetheynow'
    //noinspection GrDeprecatedAPIUsage
    compileSdk 36

    defaultConfig {
        applicationId "ru.mammoth70.wherearetheynow"
        minSdk 31
        targetSdk 36
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "MAPKIT_API_KEY", "\"${mapkitApiKey}\""
        resConfigs "en", "ru"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    //outputFileName = "WhereAreTheyNow-${variant.versionName}-${variant.buildType.name}.apk"
                    outputFileName = "WhereAreTheyNow-${variant.versionName}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig = true
    }

    packagingOptions {
        jniLibs {
            //useLegacyPackaging = true // меньше размер apk (в два раза, со 100 до 50 МБ), но больше размер приложения в памяти
            useLegacyPackaging = false  // больше размер apk, но меньше размер приложения в памяти
        }
    }

    kotlinOptions {
        //noinspection GrDeprecatedAPIUsage
        jvmTarget = '11'
    }
}

dependencies {
    implementation libs.appcompat
    //noinspection UseTomlInstead
    implementation 'com.google.android.material:material:1.14.0-alpha06'
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.play.services.location
    //noinspection UseTomlInstead,NewerVersionAvailable
    implementation 'com.yandex.android:maps.mobile:4.19.0-lite'
    implementation libs.core.ktx
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}